app FeatureBoard {
  wasp: {
    version: "^0.16.3"
  },
  title: "FeatureBoard",
  client: {
    rootComponent: import { Layout } from "@src/Layout.jsx",
  },
  auth: {
    userEntity: User,
    methods: {
      usernameAndPassword: {
      }
    },
    onAuthFailedRedirectTo: "/login",
    onAuthSucceededRedirectTo: "/features"
  },
}

route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@src/auth/Login.jsx"
}

route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import Signup from "@src/auth/Signup.jsx"
}

//#region Feedback
action createFeature {
  fn: import { createFeature } from "@src/features/actions.js",
  entities: [Feature, User]
}

action commentOnFeature {
  fn: import { commentOnFeature } from "@src/features/actions.js",
  entities: [Comment, Feature, User]
}

action deleteComment {
  fn: import { deleteComment } from "@src/features/actions.js",
  entities: [Comment, User]
}

action reactToComment {
  fn: import { reactToComment } from "@src/features/actions.js",
  entities: [Comment, CommentReaction, User]
}

action upvoteFeature {
  fn: import { upvoteFeature } from "@src/features/actions.js",
  entities: [Upvote, Feature, User]
}

action updateFeature {
  fn: import { updateFeature } from "@src/features/actions.js",
  entities: [Feature, User]
}

action updateFeatureStatus {
  fn: import { updateFeatureStatus } from "@src/features/actions.js",
  entities: [Feature, User]
}

action deleteFeature {
  fn: import { deleteFeature } from "@src/features/actions.js",
  entities: [Feature, Comment, Upvote, User]
}

query getAllFeatures {
  fn: import { getAllFeatures } from "@src/features/queries.js",
  entities: [Feature, User, Comment, Upvote]
}

query getFeatureById {
  fn: import { getFeatureById } from "@src/features/queries.js",
  entities: [Feature, Comment, Upvote, User]
}

query getUserUpvotes {
  fn: import { getUserUpvotes } from "@src/features/queries.js",
  entities: [Upvote]
}

route FeatureRoute { path: "/features", to: FeatureBoardPage }
page FeatureBoardPage {
  component: import { FeatureBoardPage } from "@src/features/pages/FeatureBoard/index.jsx"
}

route NewFeatureRoute { path: "/features/new", to: NewFeaturePage }
page NewFeaturePage {
  authRequired: true,
  component: import { NewFeaturePage } from "@src/features/pages/NewFeature/index.jsx"
}

route FeatureDetailRoute { path: "/features/:id", to: FeatureDetailPage }
page FeatureDetailPage {
  component: import { FeatureDetailPage } from "@src/features/pages/FeatureDetail/index.jsx"
}
//#endregion
